# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=pv-git
pkgver=1.8.5.r10.g1ad9c32
pkgrel=1
pkgdesc="Pipe Viewer is a terminal-based tool for monitoring the progress of data through a pipeline"
arch=('i686' 'x86_64')
url="https://www.ivarch.com/programs/pv.shtml"
license=('GPL-3.0-or-later')
depends=('glibc')
makedepends=('git' 'autoconf')
provides=("pv=$pkgver")
conflicts=('pv')
source=("${pkgname%-git}::git+https://codeberg.org/a-j-wood/pv.git")
sha256sums=('SKIP')


pkgver() {
  cd "${pkgname%-git}"

  _tag=$(git tag -l --sort -v:refname | sed '/rc[0-9]*/d' | head -n1)
  _rev=$(git rev-list --count $_tag..HEAD)
  _hash=$(git rev-parse --short HEAD)
  printf "%s.r%s.g%s" "$_tag" "$_rev" "$_hash" | sed 's/^v//'
}

build() {
  cd "${pkgname%-git}"

	autoreconf -is
  ./configure --prefix="/usr"
  make
}

check() {
  cd "${pkgname%-git}"
  make check
}

package() {
  cd "${pkgname%-git}"

  make DESTDIR="$pkgdir" install
  install -Dm644 "docs/COPYING" -t "$pkgdir/usr/share/licenses/pv"
}
